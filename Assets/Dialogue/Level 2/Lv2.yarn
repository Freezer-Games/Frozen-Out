title: Alfrio
animator_name: ALFRIO
---
Pol: Buenos días. #line:0ce2873
<<if visited("Alfrio") is false>>
	<<setanim ALFRIO Anger>>
	Alfrio: Adónde te crees que vas? Esta es mi calle. #line:085810d
	Pol: ¿Qué calle? Si es un pasillo de nieve. #line:0a73d15
	<<setanim ALFRIO Laugh_1>>
	<<setanim ALFRIO Denial>>
	Alfrio: Y encima me vacilas, ahora sí que no dejo pasar a nadie más. #line:03fbea7
	Pol: Cómo que a <i>nadie</i> más, ¿quién ha pasado por aquí? #line:0b247be
	<<setanim ALFRIO Surprise>>
	Alfrio: Ahora te interesas, pues era un pequeño tutti. #line:0a1453b
	<<setanim ALFRIO Denial>>
	Alfrio: Una pena que no puedas pasar para seguirlo... #line:0a1f9f8
	Pol: (No creo que se mueva sin motivo) #line:0a3f2df
<<else>>
	<<setanim ALFRIO Laugh_1>>
	Alfrio: Ese tutti me ha dado hambre, pero claro, no me puedo mover de aquí #line:081f9c6
<<endif>>

<<if $confitura>>
	Pol: He pensado que esto te gustaría. #line:07bb98b
	<<setanim ALFRIO Surprise>>
	Alfrio: Pero si son... #line:0541d96
	<<setanim ALFRIO Surprise>>
	Alfrio: ¿Cómo lo has conseguido...? Da igual, me voy al parque a saborear esta delicia.	 #line:05e33d3
	<<set $Guard_Moves to true>>
<<endif>>
===

title: Alof
animator_name: ALOF
---
<<if visited("Alof") is false>>
	<<setanim ALOF Awaken>>
	Pol: Buenos días. #line:05cbaba
	Pol: Huele muy bien por aquí. #line:071eb8c
	<<setanim ALOF Agree_1>>
	Alof: Gracias, son mis confituras heladas. #line:0db8fc7
	<<setanim ALOF Awaken>>
	Alof: Eso sí, no están a la venta, son sólo para mis nietos. #line:04f47b9
<<else>>
	Pol: (Esas confituras pintan bien) #line:030690b
<<endif>>

<<if $confitura is true>>
	<<setanim ALOF Awaken>>
	Alof: Esa confitura es de una textura y calidad muy similar a la mía. #line:092a3e0
	<<setanim ALOF Duda>>
	Alof: ¿Acaso alguien me está copiando la fórmula, chico? #line:071b0a7
	Pol: (...) #line:0dba937
<<endif>>
	
<<if visited("Alfrio")>>
	Pol: Quiénes eran tus nietos, por cierto? #line:048ad47
	<<setanim ALOF Agree_2>>
	Alof: Seguro que los has visto, mi Alfrio siempre anda por <i>su</i> calle. #line:066a48f
	<<setanim ALOF Duda>>
	Alof: Mi pequeño azul Fries siempre está metido en problemas, no sé dónde estará ahora. #line:0732327
<<endif>>
===

title: Fries
animator_name: FRIES, GUARDIA
---
<<if visited("Fries") is false>>
	<<setanim FRIES Denial_1>>
	<<setanim GUARDIA Laugh_1>>
	Fries: Dejadme en paz, sólo quería ir a ver a mis amigos. #line:036fd9f
	<<setanim FRIES Denial_1>>
	<<setanim GUARDIA Laugh_2>>
	Guardia: Vamos, dinos dónde guardas tu comida. #line:0661946
	<<setanim FRIES Denial_1>>
	Fries: No es vuestra, nunca os lo diré. #line:0d90f30
	<<setanim FRIES Denial_1>>
<<endif>>

<<if $fries_saved is false>>
	<<setanim GUARDIA Surprise>>
	Guardia: ¿Tienes algún problema, rojito?. #line:0c7061f
<<endif>>

<<if visited("GuardiaNevera") is false>>
	Pol: No, no, nada, solo pasaba por aquí. #line:059ad0b
<<else>>
	<<set $fries_saved to true>>
	Pol: La verdad es que parece que hay algún problema en la nevera... #line:01b008a
	Pol: Algo que ver con las <b>raciones</b>. #line:0e2a96b
	<<setanim GUARDIA Surprise>>
	Guardia: ¿Las raciones? ¡¿Mis <b>raciones</b>?! #line:03ad173
	<<setanim GUARDIA Anger>>
	Guardia: Otra vez tocando mis cosas, se lo tengo dicho muchas veces. #line:0ecd666
	Guardia: No os mováis de aquí, ahora vuelvo. #line:017d6f2
	<<set $Bully_Moves to true>>
	Pol: (Uff) #line:0ce62fc
	<<setanim FRIES Agree_1>>
	Fries: Gracias por quitármelo de encima. #line:0e7adc3
	<<setanim FRIES Agree_1>>
	Fries: Ya no puedo salir de casa con mis confituras sin que me moleste. #line:02d4545
	<<setanim FRIES Agree_1>>
	Fries: Las tengo bien guardadas, pero te voy a dar una por ayudarme. #line:0984195
	<<setanim FRIES Agree_1>>
	Pol: ¿De verdad? Vaya, gracias, las dejas en buenas manos. #line:000ecd0
<<endif>>
===

title: GuardiaNevera
animator_name: GUARDIANEVERA
---
<<if visited("GuardiaNevera") is false>>
	Pol: Buenos días, guardia. ¿Alguna novedad? #line:094d97e
	<<setanim GUARDIANEVERA Denial>>
	Guardia: No que yo sepa. #line:0253dad
	Pol: ¿Por casualidad ha visto pasar a algún tutti por aquí? #line:0840704
	<<setanim GUARDIANEVERA Anger>>
	Guardia: ¿Un tutti dices? No quiero saber nada de ellos... #line:09d769a
	Pol: ¿Qué han hecho esta vez? #line:084fa1c
	<<setanim GUARDIANEVERA Laugh_1>>
	Guardia: ¿Por qué crees que han hecho algo? Nuestras raciones están sanas y salvas... #line:089304b
	<<setanim GUARDIANEVERA Denial>>
	Guardia: Nunca las hemos perdido de vista... #line:056bd1b
	<<setanim GUARDIANEVERA Surprise>>
	Guardia: Están por aquí, sí, sí... #line:01a67f2
	Pol: (Esto huele a cubito encerrado) #line:02fd95b
<<else>>
	<<setanim GUARDIANEVERA Denial>>
	Guardia: No hemos perdido nada, y mucho menos nuestras raciones... #line:0361dd5
	<<setanim GUARDIANEVERA Denial>>
	Pol: Nunca dudo de vuestra competencia, señor. #line:092dbe2
<<endif>>
===

title: Ele
animator_name: ELE
---
<<setanim ELE Awaken>>
Ele: ¡Vaya salto ha dado! No sabia que se podía hacer eso #line:0a9e83e
Pol: (Tendré que buscar otra forma de llegar al otro lado) #line:0fc0906
===